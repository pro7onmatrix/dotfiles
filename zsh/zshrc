# Color of prompt
autoload -U colors && colors

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git fossil
zstyle ':vcs_info:*' formats ' %b '

HISTFILE=~/.cache/zsh/zsh_history
HISTSIZE=1000
SAVEHIST=1000

autoload -U compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit
_comp_options+=(globdots)		# Include hidden files.

# vi mode
bindkey -v

# Navigate tab autocompletion menu with vi keys
bindkey -M menuselect '^h' vi-backward-char
bindkey -M menuselect '^j' vi-down-line-or-history
bindkey -M menuselect '^k' vi-up-line-or-history
bindkey -M menuselect '^l' vi-forward-char

bindkey '^?' backward-delete-char

# Change cursor shape depending on vi mode:
#   - line for insert mode
#   - block for command mode
zle-keymap-select () {
    if [[ ${KEYMAP} == vicmd ]] ||
       [[ $1 = 'block' ]]; then
        printf '\e[1 q'
    elif [[ ${KEYMAP} == main ]] ||
         [[ ${KEYMAP} == viins ]] ||
         [[ ${KEYMAP} = '' ]] ||
         [[ $1 = 'beam' ]]; then
        printf '\e[5 q'
    fi
}
zle -N zle-keymap-select
zle -N zle-keymap-init
precmd() {
    promptcmd
    chpwd
    vcs_info
}

# Set title to PWD and substitute $HOME with ~
chpwd() {
    local termname=$(sed "s/\0//g" /proc/$(xdotool getwindowpid $(xdotool getactivewindow))/cmdline)
    printf "\e]2;${PWD/#$HOME/~} - $termname\a"
}

promptcmd() {
    local exitstatus="$?"
    local ttyname=$(tty)

    if [[ "$ttyname" =~ "/dev/tty" ]]; then
        PROMPT='%B%{$fg[blue]%}%~ '
    else
        PROMPT='%B%{$fg[blue]%}%~ %{$fg[red]%}${vcs_info_msg_0_}'
    fi

    if [[ "$exitstatus" == 0 ]]; then
        PROMPT+='%{$fg[green]%}'
    else
        PROMPT+='%{$fg[red]%}'
    fi

    if [[ "$ttyname" =~ "/dev/tty" ]]; then
        PROMPT+='%#%{$reset_color%}%b '
    else
        PROMPT+='%{$reset_color%}%b '
    fi
}

setopt PROMPT_SUBST
# export PROMPT='%B%{$fg[blue]%}%~ %{$fg[red]%}${vcs_info_msg_0_}%{$fg[green]%}%{$reset_color%}%b '

# create a zkbd compatible hash;
# to add other keys to this hash, see: man 5 terminfo
typeset -g -A key

key[Home]="${terminfo[khome]}"
key[End]="${terminfo[kend]}"
key[Insert]="${terminfo[kich1]}"
key[Backspace]="${terminfo[kbs]}"
key[Delete]="${terminfo[kdch1]}"
key[Up]="${terminfo[kcuu1]}"
key[Down]="${terminfo[kcud1]}"
key[Left]="${terminfo[kcub1]}"
key[Right]="${terminfo[kcuf1]}"
key[PageUp]="${terminfo[kpp]}"
key[PageDown]="${terminfo[knp]}"
key[Shift-Tab]="${terminfo[kcbt]}"

# setup key accordingly
[[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"       beginning-of-line
[[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"        end-of-line
[[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"     overwrite-mode
[[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}"  backward-delete-char
[[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"     delete-char
[[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"         up-line-or-history
[[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"       down-line-or-history
[[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"       backward-char
[[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"      forward-char
[[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"     beginning-of-buffer-or-history
[[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"   end-of-buffer-or-history
[[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}"  reverse-menu-complete

# Finally, make sure the terminal is in application mode, when zle is
# active. Only then are the values from $terminfo valid.
if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
    autoload -Uz add-zle-hook-widget
    function zle_application_mode_start { echoti smkx }
    function zle_application_mode_stop { echoti rmkx }
    add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
    add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
fi

if [[ -f "$HOME/.config/aliasrc" ]]; then
    source "$HOME/.config/aliasrc"
fi

source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
bindkey '^ ' autosuggest-accept

source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null

# FermiONs++ source
if [[ -f "$HOME/fermions/.qcsrc_local" ]] ;then
    source "$HOME/fermions/.qcsrc_local" >/dev/null
fi

if [[ -f "$HOME/.config/lf/lf-icons" ]]; then
    source "$HOME/.config/lf/lf-icons"
fi

export QT_STYLE_OVERRIDE=kvantum

